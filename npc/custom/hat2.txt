//===== Hercules Script ======================================
//= Hat Girl
//===== By: ==================================================
//= Toms and Tokeiburu
//===== Current Version: =====================================
//= 1.0
//===== Description: =========================================
//= Create hats.
//===== Additional Comments: =================================
//= 
//============================================================

function	script	ZenyToString	{
	if (getarg(1) < 1000) {
		set getarg(0), "" + getarg(1);
	}
	else if (getarg(1) < 1000000) {
		.@v = getarg(1) % 1000;
		.@o$ = "";
		
		if (.@v < 10) {
			.@o$ = "00" + .@v;
		}
		else if (.@v < 100) {
			.@o$ = "0" + .@v;
		}
		else {
			.@o$ = .@v;
		}
		
		set getarg(0), "" + getarg(1) / 1000 + "," + .@o$;
	}
	else if (getarg(1) < 1000000000) {
		.@v1 = getarg(1) % 1000;
		.@v2 = (getarg(1) / 1000) % 1000;
		.@o1$ = "";
		.@o2$ = "";
		
		if (.@v1 < 10) {
			.@o1$ = "00" + .@v1;
		}
		else if (.@v1 < 100) {
			.@o1$ = "0" + .@v1;
		}
		else {
			.@o1$ = .@v1;
		}
		
		if (.@v2 < 10) {
			.@o2$ = "00" + .@v2;
		}
		else if (.@v2 < 100) {
			.@o2$ = "0" + .@v2;
		}
		else {
			.@o2$ = .@v2;
		}
		
		set getarg(0), "" + getarg(1) / 1000000 + "," + .@o2$ + "," + .@o1$;
	}
	
	return;
}

prt_in,34,98,5	script	Headgear Master#_t	4_F_NYDHOG,{
	setarray .@hats,
		5794,	7047,100,970,10,7291,1,0,0,0,0,0,
		5785,	7291,1,970,10,719,10,0,0,0,0,-1,5000000,
		5389,   7291,1,906,100,0,0,0,0,0,0,0,0;
		
	
	.@count = 1 + 6 * 2;
	.@endIndex = 0;
	
	for (.@i = 0; .@i < getarraysize(.@hats); .@i += .@count) {
		if (.@i == 0)
			.@menu$ = getitemname(.@hats[.@i]);
		else
			.@menu$ = .@menu$ + ":" + getitemname(.@hats[.@i]);
		
		.@endIndex++;
	}
	
	mes "[Headgear Master] ";
	mes "I can make slotted Middle Head you wish for, as long as you bring me all the ingredients!";
	mes "Which Middle Head would you like to know more about?";
	next;
	
	while(1) {
		.@j = select(.@menu$ + ":End Conversation") - 1;
		
		if (.@j >= .@endIndex) {
			close;
		}
		
		//mes "[Headgear Master]";
		mes "~ Recipe for ^009900" + getitemname(.@hats[.@j * .@count]) + "^000000 ~";
		mes " ";
		
		for (.@i = 1; .@i < .@count; .@i += 2) {
			if (.@hats[.@j * .@count + .@i] > 0) {
				mes .@hats[.@j * .@count + .@i + 1] + " ^0000ff" + getitemname(.@hats[.@j * .@count + .@i]) + "^000000";
			}
			else if (.@hats[.@j * .@count + .@i] == -1) {
				callfunc("ZenyToString",@a$,.@hats[.@j * .@count + .@i + 1]);
				mes @a$ + " ^0000ffzeny^000000";
			}
		}
		
		next;
		switch(select("Create the hat:I changed my mind!")) {
			case 1:
				mes "[Headgear Master]";
				
				.@hasItems = 1;
				
				for (.@i = 1; .@i < .@count; .@i += 2) {
					if (.@hats[.@j * .@count + .@i] > 0) {
						if (countitem(.@hats[.@j * .@count + .@i]) < .@hats[.@j * .@count + .@i + 1]) {
							if (.@hasItems > 0)
								mes "^ff0000You do not have the required items.^000000 You are missing : ";
							
							mes (.@hats[.@j * .@count + .@i + 1] - countitem(.@hats[.@j * .@count + .@i])) + " ^0000ff" + getitemname(.@hats[.@j * .@count + .@i]) + "^000000";
							.@hasItems = 0;
						}
					}
					else if (.@hats[.@j * .@count + .@i] == -1) {
						if (Zeny < .@hats[.@j * .@count + .@i + 1]) {
							if (.@hasItems > 0)
								mes "^ff0000You do not have the required items.^000000 You are missing : ";
							
							callfunc("ZenyToString",@a$,.@hats[.@j * .@count + .@i + 1] - Zeny);
							mes @a$ + " ^0000ffzeny^000000";
							.@hasItems = 0;
						}
					}
				}
				
				if (.@hasItems == 0) {
					close;
				}
				
				for (.@i = 1; .@i < .@count; .@i += 2) {
					if (.@hats[.@j * .@count + .@i] > 0) {
						delitem .@hats[.@j * .@count + .@i],.@hats[.@j * .@count + .@i + 1];
					}
					else if (.@hats[.@j * .@count + .@i] == -1) {
						Zeny -= .@hats[.@j * .@count + .@i + 1];
					}
				}
				
				emotion e_oh;
				mes "^3355FF*Vroooooom~~*";
				mes "*Bzzzzzt*";
				mes "*choogachooga*";
				mes "*Kapang!*^000000";
				getitem .@hats[.@j * .@count],1;
				close;
			break;
			case 2:
				break;
		}
	}
	close;
}
