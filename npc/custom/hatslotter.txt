//===== Hercules Script ======================================
//= Hat Girl
//===== By: ==================================================
//= Toms and Tokeiburu
//===== Current Version: =====================================
//= 1.0
//===== Description: =========================================
//= Create hats.
//===== Additional Comments: =================================
//= 
//============================================================

prt_in,45,96,2	script	Headgear Master2#slot	4_F_NYDHOG,{
	setarray .@hats,
		2284,	923,20,0,0,0,0,0,0,0,0,0,0,
		5110,	526,2,7270,1,941,1,10004,1,0,0,0,0;
		
	.@count = 1 + 6 * 2;
	.@endIndex = 0;
	
	for (.@i = 0; .@i < getarraysize(.@hats); .@i += .@count) {
		if (.@i == 0)
			.@menu$ = getitemname(.@hats[.@i]);
		else
			.@menu$ = .@menu$ + ":" + getitemname(.@hats[.@i]);
		
		.@endIndex++;
	}
	
	mes "[Headgear Master] ";
	mes "Hmm...?";
	mes "You want to make a hat?";
	next;
	
	while(1) {
		.@j = select(.@menu$ + ":End Conversation") - 1;
		
		if (.@j >= .@endIndex) {
			close;
		}
		
		mes "[Headgear Master]";
		mes getitemname(.@hats[.@j * .@count]) + "?";
		
		for (.@i = 1; .@i < .@count; .@i += 2) {
			if (.@hats[.@j * .@count + .@i] > 0) {
				mes .@hats[.@j * .@count + .@i + 1] + " ^0000ff" + getitemname(.@hats[.@j * .@count + .@i]) + "^000000";
			}
		}
		
		next;
		switch(select("Make hat:I changed my mind")) {
			case 1:
				mes "[Headgear Master]";
				
				.@hasItems = 1;
				
				for (.@i = 1; .@i < .@count; .@i += 2) {
					if (.@hats[.@j * .@count + .@i] > 0) {
						if (countitem(.@hats[.@j * .@count + .@i]) < .@hats[.@j * .@count + .@i + 1]) {
							if (.@hasItems > 0)
								mes "^ff0000You do not have the required items.^000000 You are missing : ";
							
							mes (.@hats[.@j * .@count + .@i + 1] - countitem(.@hats[.@j * .@count + .@i])) + " ^0000ff" + getitemname(.@hats[.@j * .@count + .@i]) + "^000000";
							.@hasItems = 0;
						}
					}
				}
				
				if (.@hasItems == 0) {
					close;
				}
				
				for (.@i = 1; .@i < .@count; .@i += 2) {
					if (.@hats[.@j * .@count + .@i] > 0) {
						delitem .@hats[.@j * .@count + .@i],.@hats[.@j * .@count + .@i + 1];
					}
				}
				
				emotion e_oh;
				mes "^3355FF*Vroooooom~~*";
				mes "*Bzzzzzt*";
				mes "*choogachooga*";
				mes "*Kapang!*^000000";
				getitem .@hats[.@j * .@count],1;
				close;
			break;
			case 2:
				break;
		}
	}
	close;
}
