//===== Hercules Script ======================================
//= Hat Preview Girl
//===== By: ==================================================
//= ??
//= Modified by Tokeiburu
//===== Current Version: =====================================
//= 1.0
//===== Description: =========================================
//= Preview hats.
//===== Additional Comments: =================================
//= 
//============================================================

prt_in,45,96,3	script	Preview Headgear	4_F_JOB_ASSASSIN,{
    set @bottomview, getlook(3);
    set @topview, getlook(4);
    set @midview, getlook(5);

    //doesn't actually execute until just before a 'close' statement, or when the char leaves the npc's control
    addtimer 1000, strnpcinfo(3)+"::On_Leave";

INTRO:
	mes "[Preview Headgear]";
	mes "Welcome to the preview NPC!";
	mes "How can I help you?";

MainSelection:
	switch(select("View headgears from list:View headgears from item ID:Cancel")) {
		case 2:
InputItem:
			mes "Please input an item ID you would like to preview";
			input @itemid;
			
			set @equip, getiteminfo(@itemid, 5);
			set @view, getiteminfo(@itemid, 11);
			
			if(@equip > 0 && @view > 0 && (@equip & 769) != 0) {
				if(@equip & 1)
					atcommand "@changelook 3 " + @view;
				if(@equip & 256)
					atcommand "@changelook 1 " + @view;
				if(@equip & 512) 
					atcommand "@changelook 2 " + @view;
				
				dispbottom "Displaying " + getitemname(@itemid) + ".";
				
				mes "^00A000There you go!^000000";
				next;
			}
			else {
				mes "^ff0000Item not found or the item is not a headgear!^000000";
				next;
			}
			
			mes "[Preview Headgear]";
			mes "Would to like to preview something else?";
			if(select("Yes", "No") == 1) {
				goto InputItem;
			}
			
			goto MainSelection;
		case 1:
SelectItem:
				.@s = select( .alphabet_menu$ + ":Back" ) -1;
				
				if (.@s >= getarraysize(.sub_menus$)) {
					goto MainSelection;
				}
SubSelect:				
				.@sub = select(.sub_menus$[.@s] + ":Back") - 1;
				
				@itemid = .sub_menusIndexes[50 * .@s + .@sub];
				
				if (@itemid == 0) {
					goto SelectItem;
				}
				
				set @equip, getiteminfo(@itemid, 5);
				set @view, getiteminfo(@itemid, 11);
				
				if(@equip > 0 && @view > 0 && (@equip & 769) != 0) {
					if(@equip & 1)
						atcommand "@changelook 3 " + @view;
					if(@equip & 256)
						atcommand "@changelook 1 " + @view;
					if(@equip & 512) 
						atcommand "@changelook 2 " + @view;
				}
				else {
					mes "^ff0000Item not found or the item is not a headgear!^000000";
					next;
				}
				
				goto SubSelect;
		case 3:
			goto InnerLeave;
	}

InnerLeave:
	mes "See you around!";
	goto On_Leave;
OnInit:
	if (checkre(0)) {
		.@item_db$ = "item_db_re";
	} else {
		.@item_db$ = "item_db";
	}
	
	freeloop 1;
	.@total = query_sql( "SELECT DISTINCT LEFT( `name_japanese`, 1 ) AS alphabets FROM `"+ .@item_db$ +"` WHERE (`equip_locations`) & 769 != 0 AND `type` = 5 GROUP BY `name_japanese` ORDER BY alphabets;", .alphabet$ );
	
	for (.@i = 0; .@i < .@total; .@i++) {
		.alphabet_menu$ = .alphabet_menu$ + .alphabet$[.@i] +" Hats:";
		.@nb = query_sql( "SELECT `"+ .@item_db$ +"`.`id` FROM `"+ .@item_db$ +"` WHERE (`equip_locations`) & 769 != 0 AND `type` = 5 AND LEFT( `name_japanese`, 1 ) = '"+ .alphabet$[.@i] +"' GROUP BY `view` ORDER BY `name_japanese` LIMIT 128;", .@id );

		for (.@j = 0; .@j < .@nb; .@j++) {
			.sub_menusIndexes[.@i * 50 + .@j] = .@id[.@j];
			.sub_menus$[.@i] = .sub_menus$[.@i] + getitemname(.@id[.@j]) + ":";
		}
	}
	freeloop 0;
	end;
On_Leave:
    atcommand "@changelook 1 " + @topview;
    atcommand "@changelook 2 " + @midview;
    atcommand "@changelook 3 " + @bottomview;
    close;
}

