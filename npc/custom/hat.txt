//===== Hercules Script ======================================
//= Hat Girl
//===== By: ==================================================
//= Toms and Tokeiburu
//===== Current Version: =====================================
//= 1.0
//===== Description: =========================================
//= Create hats.
//===== Additional Comments: =================================
//= 
//============================================================

prt_in,38,104,5	script	Hat Maker	4_F_KHALITZBURG,{
	setarray .@hats,
		5033,	2213,1,7123,20,0,0,0,0,0,0,0,0,
		5034,	2213,1,2214,10,0,0,0,0,0,0,0,0;
	
	.@count = 1 + 6 * 2;
	.@endIndex = 0;
	
	for (.@i = 0; .@i < getarraysize(.@hats); .@i += .@count) {
		if (.@i == 0)
			.@menu$ = getitemname(.@hats[.@i]);
		else
			.@menu$ = .@menu$ + ":" + getitemname(.@hats[.@i]);
		
		.@endIndex++;
	}
	
	mes "[Hat Maker]";
	mes "Hmm...?";
	mes "You want to make a hat?";
	next;
	
	.@j = select(.@menu$ + ":End Conversation") - 1;
	
	if (.@j >= .@endIndex) {
		close;
	}
	
	mes "[Hat Maker]";
	mes getitemname(.@hats[.@j * .@count]) + "?";
	
	for (.@i = 1; .@i < .@count; .@i += 2) {
		if (.@hats[.@j * .@count + .@i] > 0) {
			mes .@hats[.@j * .@count + .@i + 1] + " " + getitemname(.@hats[.@j * .@count + .@i]);
		}
	}
	
	next;
	switch(select("Make hat:I changed my mind")) {
		case 1:
			for (.@i = 1; .@i < .@count; .@i += 2) {
				if (.@hats[.@j * .@count + .@i] > 0) {
					if (countitem(.@hats[.@j * .@count + .@i]) < .@hats[.@j * .@count + .@i + 1]) {
						mes "You do not the required items";
						close;
					}
				}
			}
			
			for (.@i = 1; .@i < .@count; .@i += 2) {
				if (.@hats[.@j * .@count + .@i] > 0) {
					if (countitem(.@hats[.@j * .@count + .@i])) {
						delitem .@hats[.@j * .@count + .@i],.@hats[.@j * .@count + .@i + 1];
					}
				}
			}
			
			emotion e_oh;
			mes "^3355FF*Vroooooom~~*";
			mes "*Bzzzzzt*";
			mes "*choogachooga*";
			mes "*Kapang!*^000000";
			getitem .@hats[.@j * .@count],1;
			close;
		break;
		case 2:
			close;
	}
	close;
}
