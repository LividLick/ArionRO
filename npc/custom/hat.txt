//===== Hercules Script ======================================
//= Hat Girl
//===== By: ==================================================
//= Toms and Tokeiburu
//===== Current Version: =====================================
//= 1.0
//===== Description: =========================================
//= Create hats.
//===== Additional Comments: =================================
//= 
//============================================================

prt_in,38,104,5	script	Headgear Master	4_F_NYDHOG,{
	setarray .@hats,
		2284,	923,20,0,0,0,0,0,0,0,0,0,0,
		5110,	526,2,7270,1,941,1,10004,1,0,0,0,0,
		2296,   2243,1,999,100,0,50000,0,0,0,0,0,0,
		5057,   2213,1,983,1,914,200,0,10000,0,0,0,0,
		5016,   1030,10,0,0,0,0,0,0,0,0,0,0,
		2214,   949,100,706,1,722,1,2213,1,0,0,0,0,
		5107,   519,50,7031,50,548,50,539,50,0,0,0,0,
		5082,   1030,921,300,0,0,0,0,0,0,0,0,0,
	    2283,   724,1,5001,1,949,200,0,0,0,0,0,0,
		5170,   5172,1,7063,100,982,1,0,0,0,0,0,0,
		5069,   1022,999,0,0,0,0,0,0,0,0,0,0,
		5001,   999,40,984,1,970,1,1003,1,0,0,0,0,
		5070,   7216,300,7097,300,2211,1,982,1,0,0,0,0,
		5071,   5010,1,5049,1,7101,10,0,0,0,0,0,0,
		5073,   2285,1,1550,1,0,0,0,0,0,0,0,0,
		5117,   731,10,748,3,982,1,0,0,0,0,0,0;
	.@count = 1 + 6 * 2;
	.@endIndex = 0;
	
	for (.@i = 0; .@i < getarraysize(.@hats); .@i += .@count) {
		if (.@i == 0)
			.@menu$ = getitemname(.@hats[.@i]);
		else
			.@menu$ = .@menu$ + ":" + getitemname(.@hats[.@i]);
		
		.@endIndex++;
	}
	
	mes "[Headgear Master] ";
	mes "Hmm...?";
	mes "You want to make a hat?";
	next;
	
	while(1) {
		.@j = select(.@menu$ + ":End Conversation") - 1;
		
		if (.@j >= .@endIndex) {
			close;
		}
		
		mes "[Headgear Master]";
		mes getitemname(.@hats[.@j * .@count]) + "?";
		
		for (.@i = 1; .@i < .@count; .@i += 2) {
			if (.@hats[.@j * .@count + .@i] > 0) {
				mes .@hats[.@j * .@count + .@i + 1] + " ^0000ff" + getitemname(.@hats[.@j * .@count + .@i]) + "^000000";
			}
		}
		
		next;
		switch(select("Make hat:I changed my mind")) {
			case 1:
				mes "[Headgear Master]";
				
				.@hasItems = 1;
				
				for (.@i = 1; .@i < .@count; .@i += 2) {
					if (.@hats[.@j * .@count + .@i] > 0) {
						if (countitem(.@hats[.@j * .@count + .@i]) < .@hats[.@j * .@count + .@i + 1]) {
							if (.@hasItems > 0)
								mes "^ff0000You do not have the required items.^000000 You are missing : ";
							
							mes (.@hats[.@j * .@count + .@i + 1] - countitem(.@hats[.@j * .@count + .@i])) + " ^0000ff" + getitemname(.@hats[.@j * .@count + .@i]) + "^000000";
							.@hasItems = 0;
						}
					}
				}
				
				if (.@hasItems == 0) {
					close;
				}
				
				for (.@i = 1; .@i < .@count; .@i += 2) {
					if (.@hats[.@j * .@count + .@i] > 0) {
						delitem .@hats[.@j * .@count + .@i],.@hats[.@j * .@count + .@i + 1];
					}
				}
				
				emotion e_oh;
				mes "^3355FF*Vroooooom~~*";
				mes "*Bzzzzzt*";
				mes "*choogachooga*";
				mes "*Kapang!*^000000";
				getitem .@hats[.@j * .@count],1;
				close;
			break;
			case 2:
				break;
		}
	}
	close;
}
